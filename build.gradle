plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
    id "systems.manifold.manifold-gradle-plugin" version "0.0.2-alpha"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

// this is for manifold preprocessor
def writeBuildPropertiesFile(List<String> minecraft_versions, int selected_minecraft_version_index) {
    StringBuilder sb = new StringBuilder()
    sb.append("# DON'T TOUCH THIS FILE, This is handled by the build script\n")
    sb.append("# This file is used for manifold preprocessor\n")
    for (int i = 0; i < minecraft_versions.size(); i++) {
        String minecraft_version = minecraft_versions[i].replace(".", "_").replace("-", "_")
        sb.append("MC_%s=%s\n".formatted(minecraft_version, i))
    }
    sb.append("SELECTED_MINECRAFT_VERSION="+selected_minecraft_version_index)

    new File(projectDir, "build.properties").text = sb.toString()
}

// save the properties from $gradle to $project so they are usable by $project.<key>
project.gradle.ext.getProperties().each { prop -> project.ext.set(prop.key, prop.value) }
writeBuildPropertiesFile(project.minecraft_versions, project.selected_minecraft_version_index)

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_mappings}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    implementation 'io.github.wasabithumb:jtoml:1.3.0'
    implementation 'io.github.wasabithumb:jtoml-serializer-reflect:1.3.0'

    annotationProcessor "systems.manifold:manifold-preprocessor:$project.manifold_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "compatible_minecraft_versions", project.compatible_minecraft_versions
    inputs.property "loader_version", project.fabric_loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "compatible_minecraft_versions": project.compatible_minecraft_versions,
                "loader_version": project.fabric_loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
