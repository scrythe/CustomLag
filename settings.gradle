pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        gradlePluginPortal()
    }
}

/** Loads the VersionProperties field for the currently selected Minecraft version. */
def loadProperties() {
    // Get all the files in "versionProperties"
    ArrayList<String> minecraft_versions = fileTree("versionProperties").files.name
    for (int i = 0; i < minecraft_versions.size(); i++) {
        // remove the ".properties" at the end to get the versions
        String version = minecraft_versions[i].replace(".properties", "")
        minecraft_versions[i] = version
    }
    // Sort so it always goes from oldest to newest
    minecraft_versions.sort((a, b) -> sortVersion(a, b))

    println("Available MC Versions: ${minecraft_versions}")

    int selected_minecraft_version_index = -1
    if (hasProperty("selected_minecraft_version")) {
        selected_minecraft_version_index = minecraft_versions.indexOf(selected_minecraft_version)
    }
    if (selected_minecraft_version_index == -1) {
        throw new GradleException("Minecraft version: $selected_minecraft_version not configured or found.\n" +
                "Either Create a ${selected_minecraft_version}.properties file in versionProperties, change the \"selected_minecraft_version\" property in gradle.properties\n" +
                "or use -Pselected_minecraft_version=\"<insert correct minecraft version properties file>\" in cmd arg to set selected_minecraft_version\n")
    }

    Properties props = new Properties()
    props.load(new FileInputStream("$rootDir/versionProperties/${selected_minecraft_version}.properties"))
    // load the properties of the selected minecraft version and save it to $gradle
    props.each { prop -> gradle.ext.set(prop.key, prop.value) }
    gradle.ext.minecraft_versions = minecraft_versions
    gradle.ext.selected_minecraft_version_index = selected_minecraft_version_index
}

static def sortVersion(String v1, String v2) {
    v1 = v1.replace("-TEST", "")
    v2 = v2.replace("-TEST", "")

    String[] v1Parts = v1.split("\\.")
    String[] v2Parts = v2.split("\\.")
    for (int i = 0; i < 3; i++) {
        int versionPart1 = Integer.parseInt(v1Parts[i])
        int versionPart2 = Integer.parseInt(v2Parts[i])
        if (versionPart1 == versionPart2) continue
        return Integer.compare(versionPart1, versionPart2)
    }
    return 0
}

loadProperties()